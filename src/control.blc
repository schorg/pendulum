import params "params"
import flow "flow" exposes Sensing, Actuation
import position "position" exposes Positioning
import standup "standup" exposes Standup
import balance "balance" exposes LqrSim
import ui "ui"

module exposes Control

activity Moving(tune: params.Position, manualLimit: params.Limit,
                moveCmd: ui.MoveEvent, sensors: Sensing)
               (motion: ui.MotionStatus, x_setpoint: float64, actuators: Actuation)
    cobegin
        repeat
            await moveCmd.present
            x_setpoint = moveCmd.x_setpoint
        end
    with
        run Positioning(tune, manualLimit, x_setpoint, sensors)(motion, actuators)
    end
end

activity SwingingUp(tune: params.Position, standupLimit: params.Limit, moves: [11]params.Move, sensors: Sensing)
                   (motion: ui.MotionStatus, x_setpoint: float64, actuators: Actuation)
    var varyingLimit: params.Limit = {v_max = standupLimit.v_max, a_max = standupLimit.a_max}
    cobegin
       run Standup(moves)(x_setpoint, varyingLimit)
    with weak
       run Positioning(tune, varyingLimit, x_setpoint, sensors)(motion, actuators)
    end
end

activity Balancing(tune: params.Balance, moveCmd: ui.MoveEvent, sensors: Sensing)
                  (motionStatus: ui.MotionStatus, x_setpoint: float64, actuators: Actuation)
    cobegin
        repeat
            await moveCmd.present
            x_setpoint = moveCmd.x_setpoint
        end
    with
        run LqrSim(tune, x_setpoint, sensors)(actuators)
    end
end


activity Control(tune: params.Control,
                 move: ui.MoveEvent, autorun: ui.AutorunEvent, 
                 sensors: flow.Sensing)
                (motion: ui.MotionStatus, actuators: flow.Actuation)
    repeat
        if not sensors.machineEnabled then
            await sensors.machineEnabled
        end
        cobegin weak
            repeat
                var x_setpoint = sensors.x_actual
                when autorun.present and autorun.on abort
                    run Moving(tune.position, tune.manualLimit, move, sensors)(motion, x_setpoint, actuators)
                end
                when autorun.present and not autorun.on abort
                    run SwingingUp(tune.position, tune.standupLimit, tune.moves, sensors)(motion, x_setpoint, actuators)
                    run Balancing(tune.balance, move, sensors)(motion, x_setpoint, actuators)
                end
            end
        with
            await not sensors.machineEnabled 
        end
    end
end
